namespace TcFairplay

open System
open System.Net.Http

module Jobs =
    let private postalCode = "8057"

    let private nightHoursRange = (22, 8)
    let private minNightTempLimit = 0.0

    let private dayHoursRange = (8, 16)
    let private minDayTempLimit = 5.0

    let private allCourts = [Court1; Court2; Court3]
    let private morning = TimeOnly(8, 0)
    let private noon = TimeOnly(12, 0)
    let private groundFrostBlockingTitle = "â„ï¸â„ï¸â„ï¸ Bodenfrost (automatische Sperre) â„ï¸â„ï¸â„ï¸"

    let groundFrostCheck (gotCourtsClient: HttpClient): unit =
        let now = DateTime.Now
        printfn "ğŸ¾ Starting job 'â† Ground Frost â†' (%s)." (formatTimeStamp ())

        // MeteoSwiss temperature prognosis
        printfn "â›… Fetching weather prognosis from MeteoSwiss for postal code %s." postalCode
        let temps = MeteoSwiss.getTemperaturePrognosis postalCode
        printfn "â›… done."

        let minTemp =
            temps[fst nightHoursRange..snd nightHoursRange + 24]
            |> List.min

        if minTemp <= minNightTempLimit then
            printfn "â„ï¸ Danger of ground frost, temperatur will drop to %2.1fÂ° C in the coming night." minTemp
            let tomorrow =
                now.AddDays (1.0)
                |> DateOnly.FromDateTime

            let tomorrowTemps = temps |> List.skip 24 |> List.take 24

            let maxTempTomorrow =
                tomorrowTemps[fst dayHoursRange..snd dayHoursRange]
                |> List.max

            let startEnd, endTime =
                if maxTempTomorrow > minDayTempLimit then
                    printfn "â˜€ï¸ Temperature will raise above 5Â° C tomorrow."
                    Some (morning, noon), "until noon"
                else
                    printfn "â›„ Temperature will stay below 5Â° C tomorrow."
                    None, ""

            // GotCourts blocking
            printfn "â›” Blocking all courts tomorrow %s on GotCourts." endTime
            try
                let blocking = {
                    Description = groundFrostBlockingTitle
                    Courts = allCourts
                    Date = tomorrow
                    StartEnd = startEnd
                    Note = sprintf "Auto-created at %s." (formatTimeStamp ())
                }
                let guids = GotCourts.createBlocking gotCourtsClient blocking

                guids |> List.iter (printfn "  â›” Blocking ID: %A")
                printfn "â›” done."
                ()
            with
                | exn ->
                    printfn "ğŸ’¥ GotCourt blocking failed."
                    printfn "ğŸ’¥ Info: %A" exn

        else
            printfn "âœ… All good, minimum temperature in the coming night: %2.1fÂ° C." minTemp

        printfn "ğŸ¾ Finished job 'â† Ground Frost â†' (%s)." (formatTimeStamp ())
        printfn ""
